# Model Driven and Data Driven sLORETA vector type inverse solutions for EEG
# By Marco Congedo, CNRS, University Grenoble Alpes.
# Last Revision: 18 Nov 2019

# Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤ CONTENT Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤ #

# centeringMatrix | common average reference operator (alias: â„Œ)
# c2cd            | current density vector given a current vector
# psfLocError     | point spread function localization error
# minnorm         | minimum norm transformation Matrix
# sLORETA         | sLORETA transformation Matrix
# eLORETA         | eLORETA transformation Matrix

module EEGtomography

using LinearAlgebra, Statistics

export
  centeringMatrix, â„Œ,
  c2cd,
  psfLocError,
  psfErrors,
  minNorm,
  sLORETA,
  eLORETA


# centering matrix (H) of dimension Ne x Ne (common average reference operator).
# given a vector of data x or a leadfield k, H*x and H*k are centered.
# The result can also be found as [i==j ? 1.0-(1/Ne) : -1/Ne for i = 1:Ne, j = 1:Ne]
centeringMatrix(Ne::Int) = I-1/Ne*(ones(Ne)*ones(Ne)')
â„Œ=centeringMatrix # alias for function centeringMatrix


# 'current to current density'
# return the current density vector (xÂ²+yÂ²+zÂ²) given a current vector
c2cd(c::Vector{R}) where R<:Real =
  if rem(length(c), 3)â‰ 0
    @warn "function `c2cd`: the length of the input vector is not a mutiple of 3." length(c)
  else
    [c[(i-1)*3+1]^2 + c[(i-1)*3+2]^2 + c[(i-1)*3+3]^2 for i = 1:length(c)Ã·3]
  end


# 'point spread function Localization Error'
# return the number of localization errors obtained by point spread functions
# given a leadfield matrix `K` and a corresponding transformation matrix `T`.
# For each column of the leadfield
psfLocError(K::Matrix{R}, T::Matrix{R}) where R<:Real =
    sum(findmax(c2cd(T*K[:, i]))[2]â‰ (i-1)Ã·3+1 for i = 1:size(K, 2))

    
# 'point spread function Errors'
# return the 3-tuple of vectors holding errors obtained at each voxel and
# each component (x, y, z)
# 1) Localization errors (bool)
#           true if the maximum cd is not located in the test location.
# 2) Spread errors (Float)
#           log(sum of cd everywhere/cd in the test location)
# 2) Equalization errors (Float)
#           uncorrected variance of the cd across all locations
function psfErrors(K::Matrix{R}, T::Matrix{R}) where R<:Real
   Nvâœ–3 = size(K, 2)
   loc=Vector{Bool}(undef, Nvâœ–3)
   spr=Vector{Float64}(undef, Nvâœ–3)
   equ=Vector{Float64}(undef, Nvâœ–3)

   for i=1:Nvâœ–3
     c=c2cd(T*K[:, i])
     loc[i]=findmax(c)[2]â‰ (i-1)Ã·3+1
     spr[i]=log(sum(c)/c[(i-1)Ã·3+1])
     equ[i]=var(c, corrected=false)
   end

    return loc, spr, equ
end

# Given a Ne x Nvâœ–3 leadfield matrix, where Ne is the number of electrodes
# and Nvâœ–3 the number of voxels times 3 (the x, y, z source components)
# return the minimum norm regularized transfer matrix with regularization `Î±`.
# if `C` is `:modelDriven` (default), compute the model driven solution,
# otherwise `C` must be the data covariance matrix and in this case compute the
# data-driven solution.
# if optional keyword argument `W` is a vector of Nvâœ–3 non-negative weights
# compute the weighted min norm regularized solution. In this case `C` must be
# equal to `:modelDriven` (default), as a weighted data-driven solution is not
# defined.
# NB if passed as a matrix, `C` must be non-singular. No check is performed.
# NB the columns of the leadfield matrix must be centered (common average ref.)
function minNorm(K::Matrix{R},
                 Î±::Real=0.,
                 C::Union{Symbol, Matrix{R}}=:modelDriven;
                 W::Union{Vector{R}, Nothing}=nothing) where R<:Real

  Ne, Nvâœ–3 = size(K)
  if W isa Vector{R}
    if length(W)â‰ Nvâœ–3
      @warn "function Loreta.minimunNorm: the length of weight vector `W` is different from the number of columns of leadfield matrix k" length(W) size(K, 2)
      return
    end

    if Î±<=0.
      @warn "function Loreta.minimunNorm: a weighted minimum norm solution can be obtained only for `Î±` positive" Î±
      return
    end

    if C isa Matrix{R}
      @warn "function Loreta.minimunNorm: cannot apply weight to a data-driven solution. Weights will be ignored"
      W=nothing
    end
  end

  if W isa Vector{R}
    w=inv.(W)
    return Î±<=0. ? (w.*K')*pinv(K*(w.*K')) : (w.*K')*pinv(K*(w.*K')+Î±*â„Œ(Ne))
  else
    return Î±<=0. ? (C==:modelDriven ? pinv(K) : K'*inv(C)) :
                  (C==:modelDriven ? K'*inv(K*K'+Î±*â„Œ(Ne)) : K'*inv(C+Î±*â„Œ(Ne)))
  end
end



# Given a Ne x Nvâœ–3 leadfield matrix, where Ne is the number of electrodes
# and Nvâœ–3 the number of voxels times 3 (the x, y, z source components)
# return the sLORETA regularized transfer matrix with regularization `Î±`.
# if `C` is `:modelDriven` (default), compute the model driven solution,
# otherwise `C` must be the data covariance matrix and in this case compute the
# data-driven solution (similar to the linearly constrained min var beamformer).
# NB if passed as a matrix, `C` must be non-singular. No check is performed.
# NB the columns of the leadfield matrix must be centered (common average ref.)
function sLORETA(K::Matrix{R},
                 Î±::Real=0.,
                 C::Union{Symbol, Matrix{R}}=:modelDriven) where R<:Real
  (Ne, Nvâœ–3), Nv = size(K), size(K, 2)Ã·3
  T = Matrix{eltype(K)}(undef, Nvâœ–3, Ne) # allocate memory for the output

  Î±<=0. ? (C==:modelDriven ? Z = pinv(K*K') : Z = pinv(C)) :
          (C==:modelDriven ? Z = pinv(K*K'+Î±*â„Œ(Ne)) : Z = pinv(C+Î±*â„Œ(Ne)))
  @inbounds for v = 1:Nv
      L = K[:, (v-1)*3+1:v*3]
      T[(v-1)*3+1:v*3, :] = (âˆšinv(L'*Z*L)) * (L'*Z)
  end

  return T
end


# Given a Ne x Nvâœ–3 leadfield matrix, where Ne is the number of electrodes
# and Nvâœ–3 the number of voxels times 3 (the x, y, z source components)
# return the eLORETA regularized transfer matrix with regularization `Î±`.
# if `C` is `:modelDriven` (default), compute the model driven solution,
# otherwise `C` must be the data covariance matrix and in this case compute the
# data-driven solution (similar to the linearly constrained min var beamformer).
# The model-driven solution is iterative; the convergence at each iteration
# is printed unless optional keyword argument `â°` is set to false.
# `tol` is the tolerance for establishing convergence; it defaults to
# the square root of `Base.eps` of the nearest type of the elements of `K`.
# This corresponds to requiring the average norm of the difference between
# the 3x3 diagonal blocks of the weight matrix in two successive iterations
# to vanish for about half the significant digits.
# NB if passed as a matrix, `C` must be non-singular. No check is performed.
# NB the columns of the leadfield matrix must be centered (common average ref.)
function eLORETA(K::Matrix{R},
                 Î±::Real=0.,
                 C::Union{Symbol, Matrix{R}}=:modelDriven,
                 tol::Real=0.,
                 â°=true) where R<:Real

  KWKt(Nv::Int, ð—ž::Vector{Matrix}, ð—ª::Vector{Matrix}) =
       sum(ð—ž[v]*ð—ª[v]*ð—ž[v]' for v=1:Nv)

  # get variables, memory and split leadfield in the leadfield for each voxel
  (Ne, Nvâœ–3), Nv = size(K), size(K, 2)Ã·3
  ð—ª = Vector{Matrix}(undef, Nv);
  ðŸ’¡, ð—ž = similar(ð—ª), similar(ð—ª)
  @inbounds for v=1:Nv ð—ž[v]=K[:, (v-1)*3+1:v*3] end
  T = Matrix{R}(undef, Nvâœ–3, Ne) # allocate memory for the output

  if C==:modelDriven
    # initialization for iterations
    @inbounds for v = 1:Nv ð—ª[v] = Matrix{R}(I, 3, 3) end
    iter=1
    maxiter=300
    tol==0 ? tolerance = âˆšeps(real(R)) : tolerance = tol

    # iterations
    while true
      Y=(Î±<=0. ? pinv(KWKt(Nv, ð—ž, ð—ª)) : pinv(KWKt(Nv, ð—ž, ð—ª)+Î±*â„Œ(Ne)))
      @inbounds for v = 1:Nv ðŸ’¡[v] = (âˆšpinv(ð—ž[v]'*Y*ð—ž[v])) end
      conv=sum(norm(ð—ª[v]-ðŸ’¡[v])/Nv for v=1:Nv)
      â° && println("iteration: ", iter, "; convergence: ", conv)
        (overRun = iter == maxiter) && @warn "function LORETA.eLORETA reached the max number of iterations before convergence:", iter
        (converged = conv <= tolerance) || overRun==true ? break :
            @inbounds for v = 1:Nv ð—ª[v]=ðŸ’¡[v] end
        iter += 1
    end # while

  else
    # compute the 3x3 blocks of the weight matrix
    Î±<=0. ? Z=pinv(C) : Z = pinv(C+Î±*â„Œ(Ne))
    @inbounds for v = 1:Nv ð—ª[v] = (âˆšpinv(ð—ž[v]'*Z*ð—ž[v])) end
  end

  # compute weighted min norm solution with 3x3 block of of the weight matrix
  Y=(Î±<=0. ? pinv(KWKt(Nv, ð—ž, ð—ª)) : pinv(KWKt(Nv, ð—ž, ð—ª)+Î±*â„Œ(Ne)))
   @inbounds for v = 1:Nv T[(v-1)*3+1:v*3, :] = (ð—ª[v]*ð—ž[v]')*Y end
  return T
end

end # module

#=
# Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤ EXAMPLES OF REAL USAGE  Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤-Â¤ #

# number of electrodes, data samples, voxels
Ne, Ns, Nv=20, 200, 3000

# fake leadfield in common average reference
K = â„Œ(Ne)*randn(Ne, Nv)

# fake data
X=randn(Ne, Ns)

# sample covariance matrix of the fake data
C=(1/Ns)*(X*X')

# fake weights for weighted minimum norm solutions
weights=abs.(randn(Nv))


Tmn = minNorm(K, 1)    # unweighted model-driven min norm with Î±=1
Tmn = minNorm(K, 10)   # unweighted model-driven min norm with Î±=10
Tmn = minNorm(K, 1; W=weights) # weighted model-driven min norm with Î±=1
Tmn = minNorm(K, 1, C) # data-driven min norm with Î±=1

TsLor = sLORETA(K, 1)     # model-driven sLORETA with Î±=1
TsLor = sLORETA(K, 10)    # model-driven sLORETA with Î±=10
TsLor = sLORETA(K, 1, C)  # data-driven sLORETA with Î±=1

TeLor = sLORETA(K, 1)     # model-driven eLORETA with Î±=1
TeLor = sLORETA(K, 10)    # model-driven eLORETA with Î±=10
TeLor = sLORETA(K, 1, C)  # data-driven eLORETA with Î±=1

=#
